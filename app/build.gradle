apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cn.turboshow.tv"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a'
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('keyStoreFile')) {
                storeFile file(keyStoreFile)
            }
            keyAlias "tbs"
            if (project.hasProperty('releasePassword')) {
                storePassword releasePassword
                keyPassword releasePassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.legacy:legacy-support-v4:$legacyVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"

    // leanback
    implementation "androidx.leanback:leanback:$leanbackVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // RXJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"

    // AutoDispose
    implementation "com.uber.autodispose:autodispose-ktx:$autodisposeVersion"
    implementation "com.uber.autodispose:autodispose-lifecycle-ktx:$autodisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-ktx:$autodisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:$autodisposeVersion"

    // moshi
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"

    // nanohttpd
    implementation "org.nanohttpd:nanohttpd:$nanohttpdVersion"

    // vlc
    implementation "org.videolan:vlc-lib-android:$libvlcVersion"

    // zip4j
    implementation "net.lingala.zip4j:zip4j:$zip4jVersion"

    // commons-io
    implementation "commons-io:commons-io:$commonsIoVersion"

    // cling
    implementation "org.fourthline.cling:cling-core:$clingVersion"

    // jetty
    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-client:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

configurations.all {
    resolutionStrategy.force "org.seamless:seamless-http:$seamlessVersion"
    resolutionStrategy.force "org.seamless:seamless-util:$seamlessVersion"
    resolutionStrategy.force "org.seamless:seamless-xml:$seamlessVersion"
}
